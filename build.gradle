apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'antlr'
apply plugin: 'eclipse'
apply plugin: 'idea'

project.archivesBaseName = 'B2Program'
project.version = '0.1.0-SNAPSHOT' // If no snapshot, adapt ANTLR parser tag to git revision
project.group = 'de.hhu.stups'

mainClassName = 'de.hhu.stups.codegenerator.CodeGenerator'

repositories {
    mavenLocal()
    jcenter()
    mavenCentral()
    maven {
        name "snapshots"
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        name "release"
        url "https://oss.sonatype.org/content/repositories/releases/"
    }
    maven { url 'https://jitpack.io' }
}

dependencies {
    antlr "org.antlr:antlr4:4.7"
    compile group: 'org.antlr', name: 'ST4', version: '4.3'
    // antlr parser dependency
    compile 'com.github.hhu-stups:antlr-parser:-SNAPSHOT' // https://github.com/hhu-stups/antlr-parser'

    compile group: 'org.clojure', name: 'clojure', version: '1.6.0'

    implementation group: "com.fatboyindustrial.gson-javatime-serialisers", name: "gson-javatime-serialisers", version: "1.1.1"
    compile group: "com.google.code.gson", name: "gson", version: "2.8.6"
    testCompile 'junit:junit:4.8.2'
}

jar {
    manifest {
        attributes(
                'Main-Class': 'de.hhu.stups.codegenerator.CodeGenerator'
        )
    }
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    manifest {
        attributes(
                'Main-Class': 'de.hhu.stups.codegenerator.CodeGenerator'
        )
    }
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}


sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets.main.java.srcDirs = ['src/main/java']

sourceSets.test.runtimeClasspath += files(sourceSets.main.java.srcDirs)


tasks.withType(JavaExec) {
    if(project.hasProperty('language') && project.hasProperty('big_integer') && project.hasProperty('deferred_set_size') && project.hasProperty('file')) {
        def arguments = []
        if(!(project.hasProperty('minint') && project.hasProperty('maxint'))) {
            arguments = [language, big_integer, -2147483648, 2147483647, deferred_set_size]
        } else {
            arguments = [language, big_integer, minint, maxint, deferred_set_size]
        }

        if(project.hasProperty('useConstraintSolving')) {
            arguments.add(useConstraintSolving)
        } else {
            arguments.add(false)
        }

        if(project.hasProperty('forModelchecking')) {
            arguments.add(forModelchecking)
        } else {
            arguments.add(false)
        }

        arguments.add(file)

        if(project.hasProperty('addition')) {
            arguments.add(addition)
        }
        args(arguments)
    }

    def env_vars = ['LD_LIBRARY_PATH'  : ("$System.env.LD_LIBRARY_PATH" + System.getProperty("path.separator") + "$projectDir/"),
                    'DYLD_LIBRARY_PATH': ("$System.env.DYLD_LIBRARY_PATH" + System.getProperty("path.separator") + "$projectDir/"),
                    'PATH'             : ("$System.env.PATH" + System.getProperty("path.separator") + "$projectDir/")]
    environment env_vars

    systemProperty "java.library.path", "./"
}


